
! Copyright (C) 2009 F. Cricchio, F. Bultmark and L. Nordstrom.
! This file is distributed under the terms of the GNU General Public License.
! See the file COPYING for license details.

subroutine fermisurfbxsf
use modmain
implicit none
! local variables
integer ik,ist,ist0,ist1
integer fnum,fnum0,fnum1
integer lst,nst,i,i1,i2,i3
real(8) vc(3,4)
! allocatable arrays
real(8), allocatable :: evalfv(:,:)
complex(8), allocatable :: evecfv(:,:,:)
complex(8), allocatable :: evecsv(:,:)
! initialise universal variables
call init0
call init1
! read density and potentials from file
call readstate
! read Fermi energy from file
call readfermi
! find the new linearisation energies
call linengy
! generate the APW radial functions
call genapwfr
! generate the local-orbital radial functions
call genlofr
! compute the overlap radial integrals
call olprad
! compute the Hamiltonian radial integrals
call hmlrad
! generate muffin-tin effective magnetic fields and s.o. coupling functions
call genbeffmt
! begin parallel loop over reduced k-points set
do ik=1,nkpt
  allocate(evalfv(nstfv,nspnfv))
  allocate(evecfv(nmatmax,nstfv,nspnfv))
  allocate(evecsv(nstsv,nstsv))
  write(*,'("Info(fermisurf): ",I6," of ",I6," k-points")') ik,nkpt
! solve the first- and second-variational secular equations
  call seceqn(ik,evalfv,evecfv,evecsv)
  deallocate(evalfv,evecfv,evecsv)
! end loop over reduced k-points set
end do
fnum0=50
fnum1=50
if (ndmag.eq.1) then
! special case of collinear magnetism
  open(50,file='FERMISURF_UP.bxsf',action='WRITE',form='FORMATTED')
  open(51,file='FERMISURF_DN.bxsf',action='WRITE',form='FORMATTED')
  fnum1=51
  ist=nstfv-nempty
  ist0=max(ist-nstfsp/2,1)
  ist1=min(ist+nstfsp/2,nstfv)
else
! spin-unpolarised and non-collinear cases
  open(50,file='FERMISURF.bxsf',action='WRITE',form='FORMATTED')
  ist=(nstfv-nempty)*nspinor
  ist0=max(ist-nstfsp/2,1)
  ist1=min(ist+nstfsp/2,nstsv)
end if
nst=ist1-ist0+1
! plotting box in Cartesian coordinates
do i=1,4
  vc(:,i)=bvec(:,1)*vclp3d(1,i)+bvec(:,2)*vclp3d(2,i)+bvec(:,3)*vclp3d(3,i)
end do
! produce the Fermi surface plot
lst=0
do fnum=fnum0,fnum1
  if ((ndmag.eq.1).and.(fnum.eq.fnum1)) lst=nstfv
  write(fnum,'(" BEGIN_INFO")')
  write(fnum,'(" # Band-XCRYSDEN-Structure-File for Fermi surface plotting")')
  write(fnum,'(" # created by Elk version ",I1.1,".",I1.1,".",I2.2)') version
  write(fnum,'(" # Launch as: xcrysden --bxsf FERMISURF(_UP/_DN).bxsf")')
  write(fnum,'("   Fermi Energy: ",G18.10)') 0.d0
  write(fnum,'(" END_INFO")')
  write(fnum,'(" BEGIN_BLOCK_BANDGRID_3D")')
  write(fnum, '(" band_energies")')
  write(fnum,'(" BANDGRID_3D_BANDS")')
  write(fnum,'(I4)') nst
  write(fnum,'(3I6)') ngridk(:)
  do i=1,4
    write(fnum,'(3G18.10)') vc(:,i)
  end do
  do ist=ist0,ist1
    write(fnum,'(" BAND: ",I4)') ist
    do i1=0,ngridk(1)-1
      do i2=0,ngridk(2)-1
        do i3=0,ngridk(3)-1
          ik=ikmap(i1,i2,i3)
          write(fnum,'(G18.10)') evalsv(ist+lst,ik)-efermi
        end do
      end do
    end do
  end do
  write(fnum,'(" END_BANDGRID_3D")')
  write(fnum,'(" END_BLOCK_BANDGRID_3D")')
  close(fnum)
end do
write(*,*)
write(*,'("Info(fermisurfbxsf):")')
if (ndmag.eq.1) then
  write(*,'(" 3D Fermi surface data written to FERMISURF_UP.bxsf and&
   & FERMISURF_DN.bxsf")')
else
  write(*,'(" 3D Fermi surface data written to FERMISURF.bxsf")')
end if
write(*,'(" for plotting with XCrysDen (Fermi energy set to zero)")')
write(*,*)
write(*,'(" Launch as: xcrysden --bxsf FERMISURF(_UP/_DN).bxsf")')
write(*,*)
return
end subroutine

